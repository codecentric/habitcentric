plugins {
    id 'org.springframework.boot' version '2.1.3.RELEASE'
    id 'org.liquibase.gradle' version '2.0.1'
    id 'java'
    id 'com.diffplug.gradle.spotless' version '3.22.0'
}

apply plugin: 'io.spring.dependency-management'
apply from: 'gradle/intTest.gradle'
apply from: 'gradle/jacoco.gradle'

group = 'de.codecentric'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '1.8'

def dbUrlDefault = 'jdbc:postgresql://localhost:5432/'
def dbUserDefault = 'postgres'
def dbPasswordDefault = 'postgres'

bootRun {
    args = ["--server.port=8180"]
}

liquibase {
    activities {
        //noinspection GroovyAssignabilityCheck
        main {
            classpath "$projectDir/src/main/resources/"
            changeLogFile '/db/changelog/db.changelog-master.yaml'
            url project.hasProperty('dbUrl') ? project.property('dbUrl') : dbUrlDefault
            username project.hasProperty('dbUser') ? project.property('dbUser') : dbUserDefault
            password project.hasProperty('dbPassword') ? project.property('dbPassword') : dbPasswordDefault
            defaultSchemaName 'hc_habit'
        }
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

spotless {
    java {
        googleJavaFormat('1.7')
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

configurations {
    testImplementation {
        exclude group: 'junit', module: 'junit'
    }
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation 'io.springfox:springfox-swagger2:2.9.2'
    implementation 'io.springfox:springfox-swagger-ui:2.9.2'
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    runtimeOnly 'org.postgresql:postgresql:42.2.5'
    runtimeOnly 'org.liquibase:liquibase-core:3.6.3'
    liquibaseRuntime 'org.liquibase:liquibase-core:3.6.3'
    liquibaseRuntime 'org.postgresql:postgresql:42.2.5'
    liquibaseRuntime("ch.qos.logback:logback-core:1.2.3")
    liquibaseRuntime("ch.qos.logback:logback-classic:1.2.3")
    testImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    intTestImplementation 'au.com.dius:pact-jvm-provider-junit5_2.12:3.6.12'
    intTestImplementation 'org.junit.jupiter:junit-jupiter:5.5.1'
    intTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    intTestImplementation 'io.rest-assured:rest-assured:3.3.0'
    intTestImplementation 'org.testcontainers:junit-jupiter:1.11.3'
    intTestImplementation 'org.testcontainers:postgresql:1.11.3'
}
