plugins {
    id 'org.springframework.boot' version '2.7.2'
    id 'org.liquibase.gradle' version '2.1.1'
    id 'java'
    id 'com.diffplug.spotless' version '6.8.0'
    id "io.freefair.lombok" version "6.5.0.3"
    id 'com.github.jk1.dependency-license-report' version '2.1'
}

apply plugin: 'io.spring.dependency-management'
apply from: '../gradle/intTest.gradle'
apply from: '../gradle/licenses.gradle'
apply from: 'gradle/jacoco.gradle'

group = 'de.codecentric'
sourceCompatibility = JavaVersion.VERSION_11

def dbUrlDefault = 'jdbc:postgresql://localhost:10001/'
def dbUserDefault = 'postgres'
def dbPasswordDefault = 'postgres'

bootRun {
}

liquibase {
    activities {
        //noinspection GroovyAssignabilityCheck
        main {
            classpath "$projectDir/src/main/resources/"
            changeLogFile '/db/changelog/db.changelog-master.yaml'
            url project.hasProperty('dbUrl') ? project.property('dbUrl') : dbUrlDefault
            username project.hasProperty('dbUser') ? project.property('dbUser') : dbUserDefault
            password project.hasProperty('dbPassword') ? project.property('dbPassword') : dbPasswordDefault
            defaultSchemaName 'hc_habit'
        }
    }
}

spotless {
    java {
        googleJavaFormat('1.7')
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

configurations {
    testImplementation {
        exclude group: 'junit', module: 'junit'
    }
}

ext {
    versions = [
            restAssured       : '4.0.0',
            junitJupiter      : '5.8.0',
            chaosMonkey       : '2.5.1',
            testcontainers    : '1.15.3',
            postgresql        : '42.2.23',
            springfox         : '2.9.2',
            liquibase         : '4.4.3',
            pactProvider      : '3.6.12',
            springCloudVersion: 'Greenwich.SR6',
            jna               : '5.8.0'
    ]
}
// override the version the spring boot dependency-management plugin would enforce
ext['junit-jupiter.version'] = versions.junitJupiter
ext['jna.version'] = versions.jna // Required for Docker on ARM

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation "io.springfox:springfox-swagger2:$versions.springfox"
    implementation "io.springfox:springfox-swagger-ui:$versions.springfox"
    implementation platform("org.springframework.cloud:spring-cloud-dependencies:$versions.springCloudVersion")
    implementation 'org.springframework.cloud:spring-cloud-starter-zipkin'
    runtimeOnly "org.postgresql:postgresql:$versions.postgresql"
    runtimeOnly "org.liquibase:liquibase-core:$versions.liquibase"
    runtimeOnly "de.codecentric:chaos-monkey-spring-boot:$versions.chaosMonkey"
    liquibaseRuntime "org.liquibase:liquibase-core:$versions.liquibase"
    liquibaseRuntime "org.postgresql:postgresql:$versions.postgresql"
    liquibaseRuntime "ch.qos.logback:logback-core"
    liquibaseRuntime "ch.qos.logback:logback-classic"
    testImplementation "org.junit.jupiter:junit-jupiter:"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    intTestImplementation "au.com.dius:pact-jvm-provider-junit5_2.12:$versions.pactProvider"
    intTestImplementation "org.junit.jupiter:junit-jupiter"
    intTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    intTestImplementation "io.rest-assured:json-path:$versions.restAssured"
    intTestImplementation("io.rest-assured:rest-assured:$versions.restAssured") {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    intTestImplementation("io.rest-assured:xml-path:$versions.restAssured") {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    intTestImplementation platform("org.testcontainers:testcontainers-bom:$versions.testcontainers")
    intTestImplementation 'org.testcontainers:junit-jupiter'
    intTestImplementation 'org.testcontainers:postgresql'
}
