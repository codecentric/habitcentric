plugins {
    id 'org.springframework.boot' version '2.7.11'
    id 'org.flywaydb.flyway' version '9.12.0'
    id 'java'
    id 'com.diffplug.spotless' version '6.13.0'
    id "io.freefair.lombok" version "6.6.3"
    id 'com.github.jk1.dependency-license-report' version '2.1'
}

apply plugin: 'io.spring.dependency-management'
apply from: '../gradle/intTest.gradle'
apply from: '../gradle/licenses.gradle'
apply from: 'gradle/jacoco.gradle'

group = 'de.codecentric'
sourceCompatibility = JavaVersion.VERSION_11

def dbUrlDefault = 'jdbc:postgresql://localhost:10002/'
def dbUserDefault = 'postgres'
def dbPasswordDefault = 'postgres'

bootRun {
}

flyway {
    url = project.hasProperty('dbUrl') ? project.property('dbUrl') : dbUrlDefault
    user = project.hasProperty('dbUser') ? project.property('dbUser') : dbUserDefault
    password = project.hasProperty('dbPassword') ? project.property('dbPassword') : dbPasswordDefault
    schemas = ['hc_track']
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

spotless {
    java {
        googleJavaFormat('1.7')
    }
}

repositories {
    mavenCentral()
}

test {
    useJUnitPlatform()
}

configurations {
    testImplementation {
        exclude group: 'junit', module: 'junit'
    }
}

ext {
    versions = [
            restAssured       : '4.5.1',
            junitJupiter      : '5.8.0',
            chaosMonkey       : '2.7.2',
            testcontainers    : '1.17.6',
            reflections       : '0.10.2',
            postgresql        : '42.5.1',
            springCloudVersion: '2021.0.3',
            jna               : '5.8.0'
    ]
}
// override the version the spring boot dependency-management plugin would enforce
ext['junit-jupiter.version'] = versions.junitJupiter
ext['jna.version'] = versions.jna // Required for Docker on ARM

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.security:spring-security-oauth2-jose'
    implementation platform("org.springframework.cloud:spring-cloud-dependencies:$versions.springCloudVersion")
    implementation 'org.springframework.cloud:spring-cloud-sleuth-zipkin'
    runtimeOnly "org.postgresql:postgresql:$versions.postgresql"
    runtimeOnly 'org.flywaydb:flyway-core'
    runtimeOnly "de.codecentric:chaos-monkey-spring-boot:$versions.chaosMonkey"
    testImplementation "org.junit.jupiter:junit-jupiter"
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation "org.reflections:reflections:$versions.reflections"
    intTestImplementation "org.junit.jupiter:junit-jupiter"
    intTestImplementation 'org.springframework.boot:spring-boot-starter-test'
    intTestImplementation "io.rest-assured:json-path:$versions.restAssured"
    intTestImplementation("io.rest-assured:rest-assured:$versions.restAssured") {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    intTestImplementation("io.rest-assured:xml-path:$versions.restAssured") {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    intTestImplementation platform("org.testcontainers:testcontainers-bom:$versions.testcontainers")
    intTestImplementation 'org.testcontainers:junit-jupiter'
    intTestImplementation 'org.testcontainers:postgresql'
}
