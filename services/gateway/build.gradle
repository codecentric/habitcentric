plugins {
    id 'org.springframework.boot' version '3.1.8'
    id 'java'
    id 'com.diffplug.spotless' version '6.25.0'
    id "io.freefair.lombok" version "8.10"
    id 'com.github.jk1.dependency-license-report' version '2.9'
}

apply plugin: 'io.spring.dependency-management'
apply from: '../gradle/intTest.gradle'
apply from: '../gradle/licenses.gradle'
apply from: 'gradle/jacoco.gradle'

group = 'de.codecentric'

bootRun {
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: 'local'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

lombok {
    version = "1.18.34"
}

spotless {
    java {
        googleJavaFormat('1.17.0')
    }
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

ext {
    springCloudVersion = '2022.0.5'
    versions = [
            restAssured   : '5.5.0',
            junitJupiter  : '5.8.0',
    ]
}
// override the version the spring boot dependency-management plugin would enforce
ext['junit-jupiter.version'] = versions.junitJupiter

tasks.named('compileJava') {
  inputs.files(tasks.named('processResources'))
}

dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
    }

    intTestImplementation "io.rest-assured:json-path:$versions.restAssured"
    intTestImplementation("io.rest-assured:rest-assured:$versions.restAssured") {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    intTestImplementation("io.rest-assured:xml-path:$versions.restAssured") {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    intTestImplementation "org.junit.jupiter:junit-jupiter"
    intTestImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
    }
    intTestImplementation('org.springframework.cloud:spring-cloud-starter-contract-stub-runner') {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-web'
    }
    intTestImplementation 'org.springframework.security:spring-security-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

// disable the jar task to prevent the plain jar from being created.
tasks.named("jar") {
  enabled = false
}
