plugins {
    id 'org.springframework.boot' version '2.7.3'
    id 'java'
    id 'com.diffplug.spotless' version '6.9.1'
    id "io.freefair.lombok" version "6.5.1"
    id 'com.github.jk1.dependency-license-report' version '2.1'
}

apply plugin: 'io.spring.dependency-management'
apply from: '../gradle/intTest.gradle'
apply from: '../gradle/licenses.gradle'
apply from: 'gradle/jacoco.gradle'

group = 'de.codecentric'
sourceCompatibility = JavaVersion.VERSION_11

bootRun {
    environment SPRING_PROFILES_ACTIVE: environment.SPRING_PROFILES_ACTIVE ?: 'local'
}

spotless {
    java {
        googleJavaFormat('1.7')
    }
}

test {
    useJUnitPlatform()
}

repositories {
    mavenCentral()
}

ext {
    springCloudVersion = '2021.0.3'
    versions = [
            restAssured   : '4.0.0',
            junitJupiter  : '5.8.0',
    ]
}
// override the version the spring boot dependency-management plugin would enforce
ext['junit-jupiter.version'] = versions.junitJupiter

tasks.named('compileJava') {
  inputs.files(tasks.named('processResources'))
}

dependencies {
    annotationProcessor "org.springframework.boot:spring-boot-configuration-processor"

    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-client'
    implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testImplementation 'org.junit.jupiter:junit-jupiter-params'
    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
    }

    intTestImplementation "io.rest-assured:json-path:$versions.restAssured"
    intTestImplementation("io.rest-assured:rest-assured:$versions.restAssured") {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    intTestImplementation("io.rest-assured:xml-path:$versions.restAssured") {
        exclude group: 'com.sun.xml.bind', module: 'jaxb-osgi'
    }
    intTestImplementation "org.junit.jupiter:junit-jupiter"
    intTestImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'junit', module: 'junit'
    }
    intTestImplementation('org.springframework.cloud:spring-cloud-starter-contract-stub-runner') {
        exclude group: 'junit', module: 'junit'
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-web'
    }
    intTestImplementation 'org.springframework.security:spring-security-test'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}
